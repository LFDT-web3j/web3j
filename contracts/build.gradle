plugins {
    id 'com.android.library'
    id 'maven-publish'
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = group
            artifactId = project.name
            version = version

            artifact("$buildDir/outputs/aar/${project.name}-release.aar")

            pom {
                withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')
                    configurations.api.allDependencies.each { dependency ->
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', dependency.group)
                        dependencyNode.appendNode('artifactId', dependency.name)
                        dependencyNode.appendNode('version', dependency.version)
                    }
                }
            }
        }
    }
    repositories {
        mavenLocal()
    }
}

android {
    namespace 'com.org.web3j'
    compileSdkVersion 33

    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 33
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        main {
            java.srcDirs = ['src/main/java']
        }
        test {
            java.srcDirs = ['src/test/java']
        }
    }
}

description 'web3j support for Ethereum Improvement Proposals (EIP\'s)'

dependencies {
    implementation project(':core')
    testImplementation project(path: ':core', configuration: 'testArtifacts')
    testImplementation 'junit:junit:4.13.2'
}

