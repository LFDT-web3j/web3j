plugins {
    id 'com.android.library'
    id 'maven-publish'
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = group
            artifactId = project.name
            version = version

            artifact("$buildDir/outputs/aar/${project.name}-release.aar")

            pom {
                withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')
                    configurations.api.allDependencies.each { dependency ->
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', dependency.group)
                        dependencyNode.appendNode('artifactId', dependency.name)
                        dependencyNode.appendNode('version', dependency.version)
                    }
                }
            }
        }
    }
    repositories {
        mavenLocal()
    }
}

android {
    namespace 'org.web3j.utils'
    compileSdkVersion 34

    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 34
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    sourceSets {
        main {
            java.srcDirs = ['src/main/java']
        }
        test {
            java.srcDirs = ['src/test/java']
        }
    }
}


description 'Minimal set of web3j utility classes'

dependencies {
    api "org.bouncycastle:bcprov-jdk18on:$bouncycastleVersion"
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.0'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.3'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.9.3'
}
//
//tasks.named("spotlessJava").configure {
//    dependsOn("spotlessGroovyGradle", "compileJava", "compileTestJava", "javadoc", "test", "jacocoTestReport")
//}
//
//tasks.named("spotlessKotlin").configure {
//    dependsOn("compileJava", "spotlessJava", "spotlessGroovyGradle", "compileTestJava", "processTestResources", "javadoc", "test", "jacocoTestReport")
//}
