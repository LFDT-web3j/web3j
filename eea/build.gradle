plugins {
    id 'com.android.library' // Android library plugin for Android compatibility
    id 'maven-publish'
    id 'signing'
    id 'de.marcphilipp.nexus-publish'
}

publishing {
    publications {
        maven(MavenPublication) {

            pom {
                name = project.name
                description.set(project.provider({ project.description }))
                url = 'https://web3j.io'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                scm {
                    def slug = project.name
                    if (project.hasProperty('rootProjectOverride')) {
                        slug = project.rootProjectOverride
                    }

                    url = "https://github.com/hyperledger-web3j/$slug"
                    connection = "scm:https://github.com/hyperledger-web3j/${slug}.git"
                    developerConnection = "scm:git://github.com/hyperledger-web3j/${slug}.git"
                }

                developers {
                    developer {
                        id = 'conor10'
                        name = 'Conor Svensson'
                        email = 'conor10@gmail.com'
                    }
                }
            }
        }
    }
}

nexusPublishing {
    repositories {
        sonatype {
            username = System.getenv('OSSRH_USERNAME')
            password = System.getenv('OSSRH_PASSWORD')
        }
    }
}


nexusStaging {
    username System.getenv('OSSRH_USERNAME')
    password System.getenv('OSSRH_PASSWORD')
    packageGroup = rootProject.group
}

signing {
    sign publishing.publications.maven
    def gpgSecretKey = System.getenv('OSSRH_GPG_SECRET_KEY')
    def password = System.getenv('OSSRH_GPG_SECRET_KEY_PASSWORD')
    useInMemoryPgpKeys(gpgSecretKey, password)
}

android {
    namespace 'org.web3j'
    compileSdkVersion 34

    defaultConfig {
        minSdkVersion 26
        targetSdkVersion 34
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    sourceSets {
        main {
            java.srcDirs = ['src/main/java']
        }
        test {
            java.srcDirs = ['src/test/java']
        }
    }
}

description 'EEA JSON-RPC API'

dependencies {
    api project(':core')
    testImplementation project(path: ':core', configuration: 'testArtifacts')
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.0'
    testImplementation project(':android-test-utils')
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.0'
}

//tasks.named("spotlessJava").configure {
//    dependsOn("spotlessGroovyGradle", "compileJava", "compileTestJava", "javadoc", "test", "jacocoTestReport")
//}
//
//tasks.named("spotlessKotlin").configure {
//    dependsOn("compileJava", "spotlessJava", "spotlessGroovyGradle", "compileTestJava", "processTestResources", "javadoc", "test", "jacocoTestReport")
//}
